if (CMAKE_VERSION VERSION_LESS 2.8.8)
  message(FATAL_ERROR "sprokit requires at least CMake version 2.8.8")
endif ()

# Version checks.
set(KWIVER_VERSION
  "@KWIVER_VERSION@")

if (KWIVER_VERSION VERSION_LESS sprokit_FIND_VERSION)
  set(sprokit_FOUND FALSE)
  return()
endif ()

##? will this work
if (sprokit_FIND_VERSION_EXACT AND
    NOT KWIVER_VERSION STREQUAL sprokit_FIND_VERSION)
  set(sprokit_FOUND FALSE)
  return()
endif ()

# Find dependent packages.
set(subpkg_required)
if (sprokit_FIND_REQUIRED)
  set(subpkg_required REQUIRED)
endif ()

set(SPROKIT_BOOST_VERSION
  "@Boost_MAJOR_VERSION@.@Boost_MINOR_VERSION@.@Boost_SUBMINOR_VERSION@")
set(SPROKIT_BOOST_MANGLE_NAMESPACE
  "@BOOST_MANGLE_NAMESPACE@")
set(SPROKIT_BOOST_USE_STATIC
  "@Boost_USE_STATIC_LIBS@")
set(SPROKIT_BOOST_INCLUDE_DIR
  "@Boost_INCLUDE_DIR@")

if (NOT Boost_INCLUDE_DIR)
  set(Boost_INCLUDE_DIR
    "@Boost_INCLUDE_DIR@")
endif ()
set(Boost_USE_STATIC_LIBS "${SPROKIT_BOOST_USE_STATIC}")
find_package(Boost "${SPROKIT_BOOST_VERSION}" EXACT ${subpkg_required}
  COMPONENTS
    chrono
    filesystem
    program_options
    system)

if (NOT Boost_FOUND)
  set(sprokit_FOUND FALSE)
  return()
endif ()

# We've found everything sprokit needs...set the variables.
include("${CMAKE_CURRENT_LIST_DIR}/sprokit-config-variables.cmake")
